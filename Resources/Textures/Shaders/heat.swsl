uniform sampler2D SCREEN_TEXTURE;

uniform highp float spatial_scalee = 22.0;
uniform highp float strength_scalee = 0.005;
uniform highp float speed_scalee = 8.0;

highp float hash(highp vec2 p)
{
    p = fract(p * vec2(123.34, 456.21));
    p += dot(p, p + 45.32);
    return fract(p.x * p.y);
}

highp float noise(highp vec2 p)
{
    vec2 i = floor(p);
    vec2 f = fract(p);
    f = f * f * (3.0 - 2.0 * f);
    
    highp float a = hash(i + vec2(0.0, 0.0));
    highp float b = hash(i + vec2(1.0, 0.0));
    highp float c = hash(i + vec2(0.0, 1.0));
    highp float d = hash(i + vec2(1.0, 1.0));
    
    return mix(mix(a, b, f.x), mix(c, d, f.x), f.y);
}

highp float fbm(highp vec2 p)
{
    highp float total = 0.0;
    highp float amp = 0.5;
    highp float freq = 1.0;

    for (int i = 0; i < 3; i++) {
        total += noise(p * freq) * amp;
        freq *= 2.0;
        amp *= 0.5;
    }
    return total;
}

void fragment()
{
    highp vec2 flow1 = vec2(0.0, TIME * speed_scalee);
    highp vec2 flow2 = vec2(TIME * 0.5, TIME * speed_scalee * 1.2);

    highp float noiseVal = fbm((UV * spatial_scalee) - flow1);
    highp float noiseVal2 = fbm((UV * spatial_scalee) - flow2 + vec2(43.0, 12.0));
    
    highp vec2 distortion = vec2(
        noiseVal - 0.5,
        noiseVal2 - 0.5 
    );

    highp float heatStrength = zTexture(UV).r*1.0;
    heatStrength = clamp(-heatStrength*heatStrength + 2.0*heatStrength, 0.0, 1.0);

    highp vec2 distortedUV = UV + (distortion * strength_scalee * heatStrength);
    
    COLOR  = texture2D(SCREEN_TEXTURE, distortedUV);
}
